// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath Plugins.gradle
        classpath Plugins.kotlin
        classpath Plugins.safeArgs
        classpath Plugins.allOpen
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(Test) {
        testLogging {
            ignoreFailures = true
            exceptionFormat 'full'
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = ['passed', 'skipped', 'failed', 'standardOut', 'standardError']
        }
    }

    // See https://github.com/mockk/mockk/issues/281
    configurations.all {
        resolutionStrategy {
            force("org.objenesis:objenesis:2.6")
        }
    }
}

subprojects {
    apply plugin: MyPlugin
    if (it.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-allopen'
    apply plugin: 'androidx.navigation.safeargs'

    android.defaultConfig.testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    allOpen {
        // allows classes to be extended in debug build
        annotation 'com.example.android.lcborecommendations.test.OpenForTesting'
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
